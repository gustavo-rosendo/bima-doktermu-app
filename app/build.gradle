buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.bima.dokterpribadimu"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 30000009
        // Major -> Millions, Minor -> Thousands, Patch -> Hundreds. E.g 1.3.72 == 1003072
        versionName "3.0.9"

        buildConfigField "String", "MAPS_BASE_URL", "\"https://maps.googleapis.com\""
        buildConfigField "String", "BILL_KEY", "\"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAj1oiLMNCvso09PcCv4RxGmtKOfYZLR4NrwQ/1FcuRX4vkfYvu9MEZA5eqUpS0NtTnHQGq0YfsAYeZ560bLweADhJUbo+nIIChOLPnnuWYE+HKbKz82jQN+ZCaN06FgSSAtmDz1JUTMVeZasn0LQ3XXa65WF7CozFB1VasyxumYmf3cOtVi/9z+tSxqS3TIxgPn6AMQvcupZ4v5g84vnW2mNdh5Jo55mfhhFko//+JbYIV5BZjyKuHTpPKUxyqPLCToy9D5PyDrK1sNRRQtUu5ihiDWVFbz8ddJCG46o6w9xIebmJQO+/QzkBtjwd/babg83+AHam0OsqfPRaGcAfIwIDAQAB\""
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/bima_indo.jks")
            storePassword "dokterpribadimu123"
            keyAlias "bima_indo"
            keyPassword "dokterpribadimu123"
        }
        release {
            storeFile file("../keystore/bima_indo.jks")
            storePassword "dokterpribadimu123"
            keyAlias "bima_indo"
            keyPassword "dokterpribadimu123"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            multiDexEnabled true
            buildConfigField "boolean", "ANALYTICS_ENABLED", "false"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "ANALYTICS_ENABLED", "true"
        }
    }

    productFlavors {
        development {
            buildConfigField "String", "BASE_URL", "\"http://developer.bimaindonesia.co.id/\""
        }

        production {
            buildConfigField "String", "BASE_URL", "\"https://bimaindonesia.co.id/\""
        }
    }

    // Enable data binding support
    dataBinding {
        enabled = true
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '24.1.1'
    final RETROFIT_2_VERSION = '2.0.0'
    final OKHTTP_3_VERSION = '3.2.0'
    final DAGGER_VERSION = '2.0.2'
    final PICASSO_VERSION = '2.5.2'
    final GSON_VERSION = '2.6.2'
    final RX_JAVA_VERSION = '1.1.0'
    final RX_ANDROID_VERSION = '1.1.0'
    final RX_LIFECYCLE_VERSION = '0.4.0'
    final BINDING_ADAPTER_VERSION = '1.1.0'
    final CRASHLYTICS_VERSION = '2.5.7'
    final FACEBOOK_SDK_VERSION = '4.10.0'
    final PLAY_SERVICE_MAPS_VERSION = '9.0.0'
    final PLAY_SERVICE_AUTH_VERSION = '9.0.0'
    final CIRCLE_IMAGE_VIEW_VERSION = '2.0.0'
    final LOCATION_TRACKER_VERSION = '3.2'
    final EASY_PERMISSION_VERSION = '0.1.5'
    final PLAY_SERVICE_ANALYTICS_VERSION = '9.0.0'
    final PLAY_SERVICES_TAGMANAGER_VERSION = '9.0.0'
    final FIREBASE_SDK_CORE_LIBRARY_VERSION = '9.0.2'
    final FIREBASE_SDK_MESSAGING_LIBRARY_VERSION = '9.0.2'
    final FIREBASE_SDK_ANALYTICS_LIBRARY_VERSION = '9.0.2'
    final FIREBASE_SDK_ADMOB_LIBRARY_VERSION = '9.0.2'
    final VIEW_PAGER_INDICATOR_VERSION = '2.4.1'
    final BOTTOMSHEET_VERSION = '1.5.1'
    final MULTIDEX_SUPPORT = '1.0.0'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'javax.annotation:jsr250-api:1.0'

    /**
     *  Support libraries
     */
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"

    /**
     *   Dependency Injections
     */
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compile "com.google.dagger:dagger:$DAGGER_VERSION"

    /**
     *  The world's most powerful, yet lightest weight crash reporting solution
     */
    compile("com.crashlytics.sdk.android:crashlytics:$CRASHLYTICS_VERSION@aar") {
        transitive = true;
    }

    /**
     *   A powerful image downloading and caching library for Android
     */
    compile "com.squareup.picasso:picasso:$PICASSO_VERSION"

    /**
     *   Gson is needed to ease JSON handling and parsing.
     */
    compile "com.google.code.gson:gson:$GSON_VERSION"

    /**
     *   The smart folks at Square open sourced their HTTP-wrapper libraries, which is awesome.
     *   So let's use it.
     */
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_2_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_2_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_3_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_3_VERSION"

    /**
     *   Most of the components in this application will communicate through ReactiveX
     *   programming model. That's why we need to use many of its libraries.
     */
    compile "io.reactivex:rxjava:$RX_JAVA_VERSION"
    compile "io.reactivex:rxandroid:$RX_ANDROID_VERSION"
    compile "com.trello:rxlifecycle:$RX_LIFECYCLE_VERSION"
    compile "com.trello:rxlifecycle-components:$RX_LIFECYCLE_VERSION"

    /**
     *  Easy way to bind collections to listviews and recyclerviews with Android Data Binding framework.
     */
    compile "me.tatarka.bindingcollectionadapter:bindingcollectionadapter:$BINDING_ADAPTER_VERSION"
    compile "me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:$BINDING_ADAPTER_VERSION"

    /**
     *  Facebook SDK
     */
    compile "com.facebook.android:facebook-android-sdk:$FACEBOOK_SDK_VERSION"

    /**
     *  Google+ Google Sign-In
     */
    compile "com.google.android.gms:play-services-auth:$PLAY_SERVICE_AUTH_VERSION"

    /**
     *  A circular ImageView for Android
     */
    compile "de.hdodenhof:circleimageview:$CIRCLE_IMAGE_VIEW_VERSION"

    /**
     *  Android helper that tracks user location
     */
    compile "com.github.quentin7b:android-location-tracker:$LOCATION_TRACKER_VERSION"

    /**
     *  Simplify Android M system permissions
     */
    compile "pub.devrel:easypermissions:$EASY_PERMISSION_VERSION"

    /**
     *  Google Play Services for Google Analytics
     */
    compile "com.google.android.gms:play-services-analytics:$PLAY_SERVICE_ANALYTICS_VERSION"
    compile files('libs/GoogleConversionTrackingSdk-2.2.4.jar')

    /**
     * Google Play Services for Google Maps
     */
    compile "com.google.android.gms:play-services-maps:$PLAY_SERVICE_MAPS_VERSION"

    /**
     *  Google Play Services for Firebase core (Analytics) library
     */
    compile "com.google.firebase:firebase-core:$FIREBASE_SDK_CORE_LIBRARY_VERSION"

    /**
     *  Google Play Services for Firebase core (Analytics) library
     */
    compile "com.google.firebase:firebase-analytics:$FIREBASE_SDK_ANALYTICS_LIBRARY_VERSION"

    /**
     *  Google Play Services for Firebase AdMob library
     */
    compile "com.google.firebase:firebase-ads:$FIREBASE_SDK_ADMOB_LIBRARY_VERSION"

    /**
     *  Firebase Messaging library
     */
    compile "com.google.firebase:firebase-messaging:$FIREBASE_SDK_MESSAGING_LIBRARY_VERSION"
    /**
     *  Google Play Services for TagManager library
     */
    compile "com.google.android.gms:play-services-tagmanager:$PLAY_SERVICES_TAGMANAGER_VERSION"

    /**
     * A memory leak detection library for Android and Java
     */
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    /**
     * Paging indicator widgets compatible with the ViewPager
     */
    compile "com.github.JakeWharton:ViewPagerIndicator:$VIEW_PAGER_INDICATOR_VERSION"

    /**
     * Android component which presents a dismissible view from the bottom of the screen
     */
    compile "com.flipboard:bottomsheet-core:$BOTTOMSHEET_VERSION"
    compile "com.flipboard:bottomsheet-commons:$BOTTOMSHEET_VERSION"

    /**
     * Add support for multidexing in debug builds
     */
    compile "com.android.support:multidex:$MULTIDEX_SUPPORT"
}

/**
 * For some weird reason, we must put apply plugin after dependencies for it to work
 */
apply plugin: 'com.google.gms.google-services'